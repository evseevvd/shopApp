buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.0.3"
        classpath "org.akhikhl.gretty:gretty:1.4.0"
        classpath 'com.h2database:h2:1.4.192'

    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.flywaydb.flyway'
apply plugin: "org.akhikhl.gretty"


flyway {
    url = 'jdbc:h2:file:./src/main/resources/shop.base'
    user = 'sa'
    schemas = ['SHOP']
    table = 'changesetlog'
    sqlMigrationPrefix = 'V_'
    sqlMigrationSeparator = '-'
    locations = [
            'filesystem:./db/changesets'
    ]
}

ext {
    spring_version = '4.3.3.RELEASE'
    spring_jpa = '1.10.1.RELEASE'
    hibernate_jpa = '4.3.8.Final'
    servlet_api = '3.0.1'
    jackson = '2.7.5'
    jstl_version = '1.2'
    h2 = '1.4.192'
    dozer = '5.5.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring зависимости
    compile("org.springframework:spring-core:${spring_version}")
    compile("org.springframework:spring-web:${spring_version}")
    compile("org.springframework:spring-webmvc:${spring_version}")
    //JSTL
    compile("javax.servlet:jstl:${jstl_version}")
    //Jackson mapping
    compile("com.fasterxml.jackson.core:jackson-databind:${jackson}")
    compile("com.fasterxml.jackson.core:jackson-core:${jackson}")
    compile("com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${jackson}")
    //Hibernate
    compile("org.hibernate:hibernate-core:${hibernate_jpa}")
    compile("org.hibernate:hibernate-entitymanager:${hibernate_jpa}")

    //JPA
    compile("org.springframework.data:spring-data-jpa:${spring_jpa}")
    //Data base driver
    compile("com.h2database:h2:${h2}")
    //Mapper
    compile("net.sf.dozer:dozer:${dozer}")

    providedCompile("javax.servlet:javax.servlet-api:${servlet_api}")

    //TODO непонятно что за костыль, но без него ругается хибер, видимо из-за того что использую java8
    compile group: 'org.threeten', name: 'threetenbp', version: '1.3.1'

}

gretty {
    servletContainer='tomcat8'
    port = 11080
    contextPath = '/shopApp'
}

task runApp(dependsOn: ['clean', 'flywayMigrate', 'appStart']) {
    println 'Start ShopApp'
}